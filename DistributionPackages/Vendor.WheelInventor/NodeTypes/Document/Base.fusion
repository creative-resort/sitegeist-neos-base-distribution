prototype(Vendor.WheelInventor:Document.Base) < prototype(Neos.Fusion:Component) {
    title = ${q(documentNode).property('title')}
    titleOverride = ${q(documentNode).property('titleOverride')}
    body = null

    renderer = Neos.Neos:Page {
        head {
            metaViewport = '<meta name="viewport" content="width=device-width">'
            stylesheets = Vendor.WheelInventor:Resource.StyleSheets

            titleTag {
                content = ${props.titleOverride ? props.titleOverride : props.title}
                content.@process.environmentPrefix {
                    expression = ${Configuration.setting('Vendor.WheelInventor.environmentPrefix') + ' ' + value}
                    @if.isSet = ${!String.isBlank(Configuration.setting('Vendor.WheelInventor.environmentPrefix'))}
                }
            }
        }

        body = ${props.body}

        bodyScripts = Vendor.WheelInventor:Resource.BodyScripts
        bodyScripts.@position = 'before closingBodyTag'
    }
}
