prototype(Sitegeist.FocusPoints:Block.Image) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = 'image'

        props {
            imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
                foregroundColor = '#9e2d39'
                baseWidth = 1000
                baseHeight = 400
            }
            imageTitle = 'title'
            imageAlt = 'image alt'
            imageFocus = 'center'
        }

        propSets {
            portrait {
                imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
                    foregroundColor = '#9e2d39'
                    baseWidth = 400
                    baseHeight = 1000
                }
            }
        }
    }

    renderer = afx`
        <div class="px-20 h-40">
            <Sitegeist.Kaleidoscope:Image
                imageSource={props.imageSource}
                imageFocus={props.imageFocus}
                srcset="320w, 400w, 600w, 800w, 1000w, 1200w, 1600w"
                sizes="(min-width: 800px) 1000px, (min-width: 480px) 800px, (min-width: 320px) 440px, 100vw"
                class={['w-full', 'h-full', 'object-cover']}
                title={props.imageTitle}
                alt={props.imageAlt}
                />
        </div>
    `

    prototype(Sitegeist.Kaleidoscope:Image) {
        renderer.@process.wrapImageFocus = Neos.Fusion:Augmenter {
            style = ${'object-position:' + props.imageFocus}
            content = ${value}
        }
    }
}
