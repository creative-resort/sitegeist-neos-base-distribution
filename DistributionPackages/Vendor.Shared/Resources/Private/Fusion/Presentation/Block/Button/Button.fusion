prototype(Vendor.Shared:Block.Button) < prototype(PackageFactory.AtomicFusion.PresentationObjects:PresentationObjectComponent) {
    @presentationObjectInterface = 'Vendor\\Shared\\Presentation\\Block\\Button\\Button'

    @styleguide {
        title = 'Button'

        @caseFactory = 'Vendor\\Shared\\Presentation\\Block\\Button\\ButtonFactory'
    }

    renderer = afx`
        <span
            class={[
                "[Block.Button (" + presentationObject.variant.value + ")]",
                "inline-flex items-center min-w-touch min-h-touch",
                presentationObject.variant.equals("regular") && "text-[blue]",
                presentationObject.variant.equals("solid") && "text-[blue]",
                presentationObject.variant.equals("download") && "bg-[grey] px-4 w-full",
                presentationObject.variant.equals("pipe") && "border-r border-brand px-8 min-h-[24px]"

            ]}
        >
            <Neos.Fusion:Tag
                tagName={presentationObject.inBackend ? 'div' : 'button'}
                attributes.class="w-full text-left"
                attributes.type='submit'
                attributes.type.@if.isSumbitButton={presentationObject.type.equals('submit') && !presentationObject.inBackend}
            >
                <div
                    class={[
                        "flex gap-3 items-center justify-between"
                    ]}
                >
                    <span>
                        <PackageFactory.AtomicFusion.PresentationObjects:Slot presentationObject={presentationObject.content} />
                    </span>
                    <span
                        class={[
                            "flex-shrink-0"
                        ]}
                        @if.1={presentationObject.icon}
                    >
                        <Vendor.Shared:Block.Icon presentationObject={presentationObject.icon}/>
                    </span>
                </div>
            </Neos.Fusion:Tag>
        </span>
    `
}
